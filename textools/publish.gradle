apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'textools'

    groupId = 'me.staufer'
    libraryName = 'textools'
    artifact = 'textools'

    libraryDescription = 'Android Library to display LaTeX equations'

    siteUrl = 'https://github.com/LeonStaufer/TeXTools'
    gitUrl = 'https://github.com/LeonStaufer/TeXTools.git'

    libraryVersion = '1.0.4'

    developerId = 'LeonStaufer'
    developerName = 'Leon Staufer'
    developerEmail = 'leon@staufer.me'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

group = groupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            groupId groupId
            artifactId artifact
            version libraryVersion

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl

            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    // Package info for BinTray
    override = true
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        version {
            name = libraryVersion
            desc = "TeX Tools $libraryVersion"
            vcsTag = libraryVersion
        }
    }
}